plugins {
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'java-library'
apply plugin: 'eclipse'

repositories {
    jcenter()
}

group = 'net.minecraftforge'
archivesBaseName = 'installer'
version = '1.0.0'
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

ext {
    MANIFEST = manifest{
        attributes('Main-Class': 'net.minecraftforge.installer.SimpleInstaller')
        attributes([
            'Specification-Title':      'Installer',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    project.version,
            'Implementation-Title':     'SimpleInstaller',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/installer/')
    }
}

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
    compile 'com.google.code.gson:gson:2.8.6'
}

compileJava {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

shadowJar {
    classifier 'fatjar'
    manifest.from(MANIFEST)
}

jar {
    manifest.from(MANIFEST)
}
